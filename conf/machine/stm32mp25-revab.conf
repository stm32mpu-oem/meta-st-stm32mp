#@TYPE: Machine
#@NAME: stm32mp25-revab
#@DESCRIPTION: Configuration for all STM32MP25 boards RevAB(EV, DK, ...)
#@NEEDED_BSPLAYERS: layers/meta-openembedded/meta-oe layers/meta-openembedded/meta-python

# Define specific familly common machine name
MACHINEOVERRIDES =. "stm32mp2common:stm32mp25common:stm32mp25revabcommon:"

include conf/machine/include/st-machine-common-stm32mp.inc
include conf/machine/include/st-machine-providers-stm32mp.inc

# =========================================================================
# SOC
# =========================================================================
STM32MP_SOC_NAME = "stm32mp25"
STM32MP_ENCRYPT_SOC_NAME:append = " stm32mp25 "

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa35"
include conf/machine/include/arm/armv8a/tune-cortexa35.inc

# =========================================================================
# boot scheme
# =========================================================================
BOOTSCHEME_LABELS = "optee"

# =========================================================================
# boot device
# =========================================================================
# Define the boot device supported
BOOTDEVICE_LABELS += "emmc"
#BOOTDEVICE_LABELS += "nand-4-256-1024"
BOOTDEVICE_LABELS += "nor-sdcard"
BOOTDEVICE_LABELS += "sdcard"

# =========================================================================
# Machine settings
# =========================================================================
# Define list of devicetree per supported storage
STM32MP_DT_FILES_EMMC   += "stm32mp257f-ev1-revB"
STM32MP_DT_FILES_SDCARD += "stm32mp257f-ev1-revB"
STM32MP_DT_FILES_NOR    += "stm32mp257f-ev1-revB"

EXTERNAL_DEVICETREE_SDCARD = "stm32mp257f-ev1-revB-ca35tdcid-ostl"
EXTERNAL_DEVICETREE_SDCARD =+ "stm32mp257f-ev1-revB-ca35tdcid-ostl-m33-examples"
EXTERNAL_DEVICETREE_EMMC = "stm32mp257f-ev1-revB-ca35tdcid-ostl"
EXTERNAL_DEVICETREE_EMMC =+ "stm32mp257f-ev1-revB-ca35tdcid-ostl-m33-examples"

STM32MP_DT_FILES_SDCARD += "${@bb.utils.contains('EXTERNAL_DT_ENABLED', '1', '${EXTERNAL_DEVICETREE_SDCARD}', '', d)}"
STM32MP_DT_FILES_EMMC += "${@bb.utils.contains('EXTERNAL_DT_ENABLED', '1', '${EXTERNAL_DEVICETREE_EMMC}', '', d)}"
# =========================================================================
# Machine features
# =========================================================================
MACHINE_FEATURES += "splashscreen"
MACHINE_FEATURES += "watchdog"
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"
MACHINE_FEATURES += "usbg0"

# GPU
MACHINE_FEATURES += "${@'gpu' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES += "openvx"
MACHINE_FEATURES += "opencl"
MACHINE_FEATURES += "vulkan"

# M33
MACHINE_FEATURES += "m33copro"

# Bluetooth
BLUETOOTH_LIST += ""
# Wifi
WIFI_LIST += "linux-firmware-rtl8192ce"
# PCIe
PCI_LIST += "linux-firmware-rtl8168"

# activate external dt
EXTERNAL_DT_ENABLED = "1"

# =========================================================================
# TEMPORARY HACK
# =========================================================================
# TF-A: configuration not available
#TF_A_CONFIG:remove = "uart"
#TF_A_CONFIG:remove = "usb"

# Flashlayout
# No metadata generated and supported for the moment
STM32MP_METADATA_DATA ?= ""

# GPU have vulkan
DISTRO_FEATURES:append = " ${@bb.utils.contains('DISTRO', 'openstlinux-eglfs', '', 'vulkan', d)} "
DISTRO_FEATURES:append = " opencl "


# --------------------------------------
# U-BOOT
# --------------------------------------
# STM32mp25 revab
# STM32mp25
UBOOT_CONFIG:append:stm32mp25revabcommon = " ${@bb.utils.contains_any('BOOTSCHEME_LABELS', 'optee', 'trusted_stm32mp25', '', d)} "
UBOOT_CONFIG[trusted_stm32mp25] = "stm32mp25_revA_defconfig,,u-boot.dtb"

# --------------------------------------
# fip
# --------------------------------------
FIP_BL31_ENABLE = "1"

# --------------------------------------
# Image
# --------------------------------------
# Proposed value for vendorfs is 48MB
STM32MP_VENDORFS_SIZE = "49152"
